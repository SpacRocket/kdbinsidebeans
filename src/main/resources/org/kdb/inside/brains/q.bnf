{
  parserClass="org.kdb.inside.brains.parser.QParser"

  extends="org.kdb.inside.brains.psi.impl.QPsiElementImpl"
  implements="org.kdb.inside.brains.psi.QPsiElement"

  psiClassPrefix="Q"
  psiImplClassSuffix="Impl"
  psiPackage="org.kdb.inside.brains.psi"
  psiImplPackage="org.kdb.inside.brains.psi.impl"

  tokenTypeClass="org.kdb.inside.brains.psi.QTokenType"
  elementTypeClass="org.kdb.inside.brains.psi.QElementType"
  elementTypeHolderClass="org.kdb.inside.brains.psi.QTypes"

  tokens=[
    nill="(::)"
    colon=':'
    comma=',' // Double meaning here - splitting arguments in a table or an operator
    semicolon=';'
    bracket_open='['
    bracket_close=']'
    brace_open='{'
    brace_close='}'
    paren_open='('
    paren_close=')'

    line_break="Line break"
    line_space="Line space"

    command_import="import file command"
    command_context="changing context command"
    command_system="internal or external system command"
    command_arguments="internal or external command arguments"

    function_import="import function"

    type_cast_pattern="type cast pattern"

    operator="operator"
    iterator="iterator"

    mode_pattern="mode name"
    symbol_pattern="symbol pattern"
    variable_pattern="Variable is"
    file_path_pattern="File path is"

    unary_function="unary function"
    binary_function="binary function"
    complex_function="complex function"

    query_by="query by"
    query_type="query type"
    query_from="query from"

    atom="atom"
    vector="vector"

    char="char"
    string="string"

    control_pattern="control keyword"
    condition_pattern="condition keyword"

    line_comment='line comment'
    block_comment='block comment'
  ]
}

root ::= top*

private separator ::= semicolon | line_break
private end_of_line ::= !(line_break)

private top ::= context | mode | top_in_context
private top_in_context ::= import_command | command | expression | separator

symbol ::= symbol_pattern
private symbols ::= symbol symbol+

var_reference ::= variable_pattern {
 implements="org.kdb.inside.brains.psi.QVariable"
 mixin="org.kdb.inside.brains.psi.impl.QVariableReferenceImpl"
}

var_declaration ::= variable_pattern {
 implements=["org.kdb.inside.brains.psi.QVariable" "com.intellij.psi.PsiNameIdentifierOwner" "com.intellij.psi.NavigatablePsiElement"]
 mixin="org.kdb.inside.brains.psi.impl.QVariableDeclarationImpl"
}

// Import supporting
import_file ::= file_path_pattern { recoverWhile = end_of_line }
import_command ::= command_import line_space import_file {
 pin = 1
 implements="org.kdb.inside.brains.psi.QImport"
 mixin="org.kdb.inside.brains.psi.impl.QImportCommandBase"
 }

import_system ::= function_import string {
 implements="org.kdb.inside.brains.psi.QImport"
 mixin="org.kdb.inside.brains.psi.impl.QImportFunctionBase"
 }


fake assignment ::= var_declaration? arguments? expression? {
 methods = [variable="var_declaration"]
}
fake column_assignment ::= var_declaration? expression? {
 extends = assignment
 methods = [variable="var_declaration"]
}

assignment_operator ::= operator colon | colon colon | colon

variable_assignment ::= var_declaration [arguments] assignment_operator expression {
 pin = 3
 extends = assignment
 methods = [variable="var_declaration"]
}

inner table_column ::= (var_declaration colon expression) | expression  {
 pin(".*") = 2
 extends = column_assignment
 methods = [variable="var_declaration"]
 }
private table_cols ::= table_column (semicolon table_column)*
fake table_columns ::= table_cols {
 methods = [columns="table_column"]
}
inner key_columns ::= table_cols {
 implements="table_columns"
 methods = [columns="table_column"]
}
inner value_columns ::= table_cols {
 implements="table_columns"
 methods = [columns="table_column"]
}
table ::= paren_open bracket_open [key_columns] bracket_close [value_columns] paren_close {
 pin = 2
}

fake psi_expression ::= {}

query_expression ::= statement+ {
 extends = psi_expression
}

inner query_column ::= (var_declaration colon query_expression) | query_expression  {
 pin(".*") = 2
 extends = column_assignment
 methods = [variable="var_declaration"]
}
private query_cols ::= query_column (comma query_column)*
fake query_columns ::= query_cols
query_groups ::= query_cols {
 implements="query_columns"
}
query_selects ::= query_cols {
 implements="query_columns"
}
// - not mistake. WHERE goes into the final expression here and belong to it by Q grammar
// - we do where fist and then select from the result what we need.
query ::= query_type [query_selects] (query_by query_groups)? (query_from expression) {
 pin(".*")=1
 methods = [columns="query_selects" groups="query_groups"]
 }

// Context: \d ...
private context_name ::= var_declaration
context ::= command_context (line_space+ context_name)? (line_break top_in_context*)? {
 pin = 1
 mixin = "org.kdb.inside.brains.psi.impl.QPsiContextImpl"
 methods = [variable="var_declaration"]
 }

// Command - anything with \...
command_params ::= command_arguments {recoverWhile = end_of_line}
command ::= command_system (line_space+ command_params)? {
 pin = 1
 methods = [parameters="command_params"]
}

// In mode we can't use line_break as a separator
private mode_content ::= semicolon* expression (semicolon+ expression)* semicolon* {
 recoverWhile = end_of_line
}
mode ::= mode_pattern [mode_content] { pin = 1 } // Mode: [a-Z]) .....

parameters ::= bracket_open [var_declaration (semicolon var_declaration)*] bracket_close {
 methods = [variables="var_declaration"]
}
lambda ::= brace_open [parameters] [code_block] brace_close {pin = 1}

arguments ::= bracket_open [code_block] bracket_close {pin = 1}// [...]
code_block ::= separator* expression (separator+ expression)* separator*

control ::= control_pattern arguments { pin = 1} // if/else/while and so on
condition ::= condition_pattern arguments { pin = 1} // $[...]

//  `$ or `...$ - it's a type cast, not a symbol
type_cast ::= type_cast_pattern

//dict ::= key_statement "!" expression { pin = 2}
list ::= vector | symbols | string | mix_list
private mix_list ::= paren_open [expression] (semicolon+ [expression])+ semicolon* paren_close { pin=3 }

parentheses ::= paren_open expression paren_close

private unary_invokable ::= unary_function | lambda | var_reference | list | parentheses
private binary_invokable ::= binary_function | complex_function | ((operator | iterator) colon?)
invoke ::= (unary_invokable | binary_invokable) iterator? arguments+

private function ::= unary_function | binary_function | complex_function | lambda
private statement ::= control | condition | type_cast | variable_assignment | invoke | import_system | var_reference | table | list | parentheses | nill | atom | symbol | char | arguments | function | operator | iterator | colon colon?

expression ::= query | (statement | comma)+ query? {
 extends = psi_expression
}
