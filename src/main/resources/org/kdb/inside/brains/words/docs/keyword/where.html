<p><em>Copies of indexes of a list or keys of a dictionary</em></p>
<pre><code class="language-txt">where x    where[x]
</code></pre>
<p>Where <code>x</code> is a:</p>
<h2 id="vector-of-non-negative-integers">Vector of non-negative integers</h2>
<p>returns a vector containing, for each item of <code>x</code>, that number of copies of its index. </p>
<pre><code class="language-q">q)where 2 3 0 1
0 0 1 1 1 3
q)raze x #' til count x:2 3 0 1
0 0 1 1 1 3
</code></pre>
<p>Where <code>x</code> is boolean, the result is the indices of the 1s. Thus <code>where</code> is often used after a
    logical test:</p>
<pre><code class="language-q">q)where 0 1 1 0 1
1 2 4
q)x:1 5 6 8 11 17 20 21
q)where 0 = x mod 2        / indices of even numbers
2 3 6
q)x where 0 = x mod 2      / select even numbers from list
6 8 20
</code></pre>
<h2 id="dictionary-whose-values-are-non-negative-integers">Dictionary whose values are non-negative integers</h2>
<p>returns a list of keys repeated as many times as the corresponding value. </p>
<pre><code class="language-q">q)d:`amr`ibm`msft!2 3 1
q)where d
`amr`amr`ibm`ibm`ibm`msft
q)where 2 3 0 1               / usual operation on integer list
0 0 1 1 1 3
q)where 0 1 2 3 ! 2 3 0 1     / same on dictionary with indices as keys
0 0 1 1 1 3
</code></pre>
<div class="admonition tip">
    <p class="admonition-title">Insight</p>
    <p>If a list is viewed as a mapping from indexes to entries, than the definition for the integer list above is
        merely a special case.</p>