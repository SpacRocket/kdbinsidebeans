<p><em>Is null</em></p>
<pre><code class="language-txt">null x     null[x]
</code></pre>
<p>Returns <code>1b</code> where <code>x</code> is null.</p>
<p>Applies to all data types except enums, and to items of lists, dict values and table columns.</p>
<p><code>null</code> is an atomic function.</p>
<pre><code class="language-q">q)null 0 0n 0w 1 0n
01001b

q)where all null ([] c1:`a`b`c; c2:0n 0n 0n; c3:10 0N 30)
,`c2
</code></pre>
<p>Enums always show as non-null.</p>
<pre><code class="language-q">q)a:``a
q)`=`a$`            / non-enumerated and enumerated null symbol show as equivalent
1b
q)null`             / null symbol behaves as expected
1b
q)null`a$`          / enumeration of null symbol does not
0b
</code></pre>
<p>The intention was not to have nulls in the enums. That value is used to indicate <em>out of range</em>. (Think of
    them as a way to represent foreign keys.) To test for an enumeration backed by a null symbol, one can use the
    equality test â€“ but at the cost of CPU cycles:</p>
<pre><code class="language-q">q)a:10000000?`8
q)v:`a$a
q)\ts null v
18 16777344
q)\ts `=v
66 268435648
</code></pre>